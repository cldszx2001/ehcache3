/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *  `
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'org.asciidoctor.convert' version '1.6.0'
  id 'org.kordamp.gradle.livereload' version '0.2.1'
  id 'com.github.jruby-gradle.base' version '1.6.0'
}

dependencies {
  gems 'rubygems:asciidoctor-diagram:1.5.15'
}

configurations.asciidoctor {
  resolutionStrategy {
    force 'com.github.jnr:jnr-enxio:0.19'
    force 'com.github.jnr:jnr-posix:3.0.49'
    force 'com.github.jnr:jnr-constants:0.9.12'
  }
}

task copyCSS(type: Copy) {
  from ('css') {
    include '**'
  }
  into("${buildDir}/asciidoc/user/css")
}

asciidoctor.dependsOn copyCSS

asciidoctor {
  dependsOn jrubyPrepare
  gemPath = jrubyPrepare.outputDir
  requires = ['asciidoctor-diagram']
  separateOutputDirs = false
  attributes 'skip-front-matter': 'true'

  resources {
    from('fonts') {
      include '*'
      into('./user/fonts')
    }
  }
}

liveReload {
  docRoot asciidoctor.outputDir.canonicalPath
}

dependencyCheck {
  skip = true
}

